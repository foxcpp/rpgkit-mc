plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT' apply false
}

def getDate() {
    return new Date().format('yyyy.MM.dd')
}

allprojects {
    group = "com.github.foxcpp.rpgkitmc"
    version = "0.1." + getDate()

    apply plugin: 'fabric-loom'

    repositories {
        maven { url 'https://maven.ladysnake.org/releases' }
        maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
        maven { url "https://maven.shedaniel.me/" }
        maven { url "https://maven.terraformersmc.com/releases/" }
        maven { url 'https://maven.kosmx.dev/' }
        maven { url 'https://maven.nucleoid.xyz/' }
        maven { url 'https://api.modrinth.com/maven' }
    }

    dependencies {
        minecraft "com.mojang:minecraft:1.20.1"
        mappings "net.fabricmc:yarn:1.20.1+build.10:v2"
        modImplementation "net.fabricmc:fabric-loader:0.14.22"
        modImplementation "net.fabricmc.fabric-api:fabric-api:0.86.1+1.20.1"

        modImplementation 'software.bernie.geckolib:geckolib-fabric-1.20:4.2'

        modImplementation "dev.emi:trinkets:3.7.1"

        modApi("me.shedaniel.cloth:cloth-config-fabric:11.1.106") {
            exclude(group: "net.fabricmc.fabric-api")
            exclude(group: "net.fabricmc")
        }
        include modImplementation("dev.kosmx.player-anim:player-animation-lib-fabric:1.0.2-rc1+1.20")
        modImplementation "maven.modrinth:better-combat:1.7.2+1.20.1-fabric"

        modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-chunk:5.2.2"
        modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:5.2.2"
        include "dev.onyxstudios.cardinal-components-api:cardinal-components-chunk:5.2.2"
        include "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:5.2.2"

        modImplementation(include("eu.pb4:placeholder-api:2.1.2+1.20.1"))

        implementation group: 'net.objecthunter', name: 'exp4j', version: '0.4.8'
    }

    processResources {
        inputs.property "version", project.version

        filteringCharset "UTF-8"

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release = 17
    }

    java {
        if (JavaVersion.current() < JavaVersion.toVersion(17)) {
            toolchain.languageVersion = JavaLanguageVersion.of(17)
        }
        withSourcesJar()
    }

    tasks.withType(AbstractArchiveTask).configureEach {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }


    jar {
        from("LICENSE") {
            rename { "${it}_rpgkit" }
        }
    }
}

subprojects {
    version = rootProject.version
    javadoc.enabled = false

    afterEvaluate {
        // Disable the gen sources task on sub projects
        genSourcesWithFernFlower.enabled = false
        genSourcesWithCfr.enabled = false
    }
}
subprojects.each { remapJar.dependsOn("${it.path}:remapJar") }

dependencies {
    afterEvaluate {
        subprojects.each {
            api project(path: ":${it.name}", configuration: "namedElements")
            include project("${it.name}:")
        }
    }
}
